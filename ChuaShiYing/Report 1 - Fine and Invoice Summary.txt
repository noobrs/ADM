SET SERVEROUTPUT ON;
SET LINESIZE 200;
SET PAGESIZE 200;

CREATE OR REPLACE PROCEDURE prc_fine_invoice_all_explicit (
    p_start_date IN DATE,
    p_end_date   IN DATE
) IS
    TYPE status_table_type IS TABLE OF VARCHAR2(10);
    status_list status_table_type := status_table_type('PAID', 'UNPAID');

    CURSOR cur_fine_by_status(p_status VARCHAR2) IS
        SELECT 
            f.FineID, f.FineAmount, f.FineStatus, f.IssueDate, f.Remark,
            f.InvoiceID, 
            i.MemberID, i.PaymentMethod, i.PaymentDate, i.TotalAmount
        FROM Fine f
        LEFT JOIN Invoice i ON f.InvoiceID = i.InvoiceID
        WHERE f.FineStatus = p_status 
          AND (f.IssueDate BETWEEN p_start_date AND p_end_date)
        ORDER BY f.IssueDate;

    -- Cursor variables
    v_fine_id        Fine.FineID%TYPE;
    v_fine_amount    Fine.FineAmount%TYPE;
    v_fine_status    Fine.FineStatus%TYPE;
    v_issue_date     Fine.IssueDate%TYPE;
    v_remark         Fine.Remark%TYPE;
    v_invoice_id     Invoice.InvoiceID%TYPE;
    v_member_id      Invoice.MemberID%TYPE;
    v_payment_method Invoice.PaymentMethod%TYPE;
    v_payment_date   Invoice.PaymentDate%TYPE;
    v_total_amount   Invoice.TotalAmount%TYPE;

    -- Aggregates
    v_total_invoice_amt NUMBER := 0;
    v_total_fine_amt    NUMBER := 0;
    v_total_records     NUMBER := 0;

    v_cat_invoice_amt   NUMBER := 0;
    v_cat_fine_amt      NUMBER := 0;
    v_cat_records       NUMBER := 0;
BEGIN
    DBMS_OUTPUT.PUT_LINE(RPAD('=', 145, '='));
    DBMS_OUTPUT.PUT_LINE('<<-- Fine Summary Report (Including Unpaid) -->>' || CHR(10));
    DBMS_OUTPUT.PUT_LINE('Report Period: ' || TO_CHAR(p_start_date, 'DD-MON-YYYY') || ' to ' || TO_CHAR(p_end_date, 'DD-MON-YYYY'));
    DBMS_OUTPUT.PUT_LINE(RPAD('=', 145, '='));

    FOR i IN 1 .. status_list.COUNT LOOP
        v_cat_invoice_amt := 0;
        v_cat_fine_amt := 0;
        v_cat_records := 0;

        DBMS_OUTPUT.PUT_LINE(CHR(10) || '>> ' || status_list(i) || ' FINES' || CHR(10));
        DBMS_OUTPUT.PUT_LINE(
            RPAD('InvoiceID', 12) || RPAD('MemberID', 12) || RPAD('FineID', 12) ||
            RPAD('Amount', 10) || RPAD('Status', 8) || RPAD('IssueDate', 14) ||
            RPAD('PayDate', 14) || RPAD('Method', 15) || RPAD('InvoiceTotal', 15) || 'Remark'
        );
        DBMS_OUTPUT.PUT_LINE(RPAD('-', 145, '-'));

        -- Open explicit cursor
        OPEN cur_fine_by_status(status_list(i));

        LOOP
            FETCH cur_fine_by_status INTO 
                v_fine_id, v_fine_amount, v_fine_status, v_issue_date, v_remark,
                v_invoice_id, v_member_id, v_payment_method, v_payment_date, v_total_amount;

            EXIT WHEN cur_fine_by_status%NOTFOUND;

            DBMS_OUTPUT.PUT_LINE(
                RPAD(NVL(v_invoice_id, '-'), 12) ||
                RPAD(NVL(v_member_id, '-'), 12) ||
                RPAD(v_fine_id, 12) ||
                RPAD(TO_CHAR(v_fine_amount, '9990.00'), 10) ||
                RPAD(v_fine_status, 8) ||
                RPAD(TO_CHAR(v_issue_date, 'DD-MON-YY'), 14) ||
                RPAD(NVL(TO_CHAR(v_payment_date, 'DD-MON-YY'), '-'), 14) ||
                RPAD(NVL(v_payment_method, '-'), 15) ||
                RPAD(NVL(TO_CHAR(v_total_amount, '9990.00'), '-'), 15) ||
                v_remark
            );

            v_cat_fine_amt     := v_cat_fine_amt + v_fine_amount;
            v_cat_invoice_amt  := v_cat_invoice_amt + NVL(v_total_amount, 0);
            v_cat_records      := v_cat_records + 1;
            v_total_fine_amt   := v_total_fine_amt + v_fine_amount;
            v_total_invoice_amt:= v_total_invoice_amt + NVL(v_total_amount, 0);
            v_total_records    := v_total_records + 1;
        END LOOP;

        CLOSE cur_fine_by_status;

        DBMS_OUTPUT.PUT_LINE(RPAD('-', 145, '-'));
        DBMS_OUTPUT.PUT_LINE(
            status_list(i) || ' Fine Records: ' || v_cat_records ||
            ' | Subtotal Fine: RM ' || TO_CHAR(v_cat_fine_amt, '9990.00') ||
            ' | Subtotal Invoice: RM ' || TO_CHAR(v_cat_invoice_amt, '9990.00')
        );
        DBMS_OUTPUT.PUT_LINE(RPAD('=', 145, '='));
    END LOOP;

    DBMS_OUTPUT.PUT_LINE('Total Records: ' || v_total_records ||
                         ' | Total Fine Amount: RM ' || TO_CHAR(v_total_fine_amt, '9990.00') ||
                         ' | Total Invoice Amount: RM ' || TO_CHAR(v_total_invoice_amt, '9990.00'));
    DBMS_OUTPUT.PUT_LINE(CHR(10) || '<<-- End of Report -->>');
    DBMS_OUTPUT.PUT_LINE(RPAD('=', 145, '='));
END;
/

EXEC prc_fine_invoice_nested_all(TO_DATE('01-01-2023','DD-MM-YYYY'), TO_DATE('31-12-2025','DD-MM-YYYY'));