SET SERVEROUTPUT ON;
SET LINESIZE 120
SET PAGESIZE 120

CREATE OR REPLACE PROCEDURE prc_member_loan_detail(p_member_id IN CHAR) IS
    v_loan_id Loan.LoanID%TYPE;

    CURSOR loan_cur IS
        SELECT LoanID, LoanDate, DueDate
        FROM Loan
        WHERE MemberID = p_member_id
        ORDER BY LoanDate DESC;

    CURSOR loan_detail_cur IS
        SELECT ld.BookID, b.Title, ld.Status,
               NVL(TO_CHAR(ReturnDate, 'DD-MON-YYYY'), 'N/A') AS ReturnDateStr,
               NVL(ld.ExtendedDueDate, l.DueDate) AS CurrentDueDate
        FROM LoanDetail ld
        JOIN Book b ON b.BookID = ld.BookID
        JOIN Loan l ON l.LoanID = ld.LoanID
        WHERE ld.LoanID = v_loan_id;

    loan_rec loan_cur%ROWTYPE;
    detail_rec loan_detail_cur%ROWTYPE;
    v_total_loan     NUMBER(3);
    v_total_book     NUMBER(1);

BEGIN
    DBMS_OUTPUT.PUT_LINE(RPAD('=', 115, '='));
    DBMS_OUTPUT.PUT_LINE(LPAD('MEMBER LOAN DETAIL REPORT', 60));
    DBMS_OUTPUT.PUT_LINE(LPAD('Generated on: ' || TO_CHAR(SYSDATE, 'DD-MON-YYYY HH:MI AM'), 70));
    DBMS_OUTPUT.PUT_LINE(RPAD('-', 115, '-'));
    DBMS_OUTPUT.PUT_LINE('Member ID : ' || p_member_id);
    DBMS_OUTPUT.PUT_LINE(RPAD('=', 115, '='));

    OPEN loan_cur;
    v_total_loan := 0;
    LOOP
        FETCH loan_cur INTO loan_rec;
        EXIT WHEN loan_cur%NOTFOUND;

        v_loan_id := loan_rec.LoanID;

        DBMS_OUTPUT.PUT_LINE('Loan ID    : ' || loan_rec.LoanID);
        DBMS_OUTPUT.PUT_LINE('Loan Date  : ' || TO_CHAR(loan_rec.LoanDate, 'DD-MON-YYYY'));
        DBMS_OUTPUT.PUT_LINE('Due Date   : ' || TO_CHAR(loan_rec.DueDate, 'DD-MON-YYYY'));
        DBMS_OUTPUT.PUT_LINE(RPAD('-', 115, '-'));
        DBMS_OUTPUT.PUT_LINE(RPAD('Book ID', 15) || RPAD('Title', 50) || RPAD('Status', 15) || RPAD('Return Date', 15) || 'Due Date');
        DBMS_OUTPUT.PUT_LINE(RPAD('-', 115, '-'));

        OPEN loan_detail_cur;
        v_total_book := 0;
        
        LOOP
            
            FETCH loan_detail_cur INTO detail_rec;
            EXIT WHEN loan_detail_cur%NOTFOUND;

            DBMS_OUTPUT.PUT_LINE(
                RPAD(detail_rec.BookID, 15) ||
                RPAD(detail_rec.Title, 50) ||
                RPAD(detail_rec.Status, 15) ||
                RPAD(detail_rec.ReturnDateStr, 15) ||
                TO_CHAR(detail_rec.CurrentDueDate, 'DD-MON-YYYY')
            );
            
            v_total_book:= v_total_book + 1;
        END LOOP;
        CLOSE loan_detail_cur;

        DBMS_OUTPUT.PUT_LINE(RPAD('-', 115, '-'));
        DBMS_OUTPUT.PUT_LINE('Total of Books: ' || v_total_book);
        DBMS_OUTPUT.PUT_LINE(CHR(10));
        v_total_loan := v_total_loan + 1;
    END LOOP;
    CLOSE loan_cur;

    DBMS_OUTPUT.PUT_LINE(RPAD('=', 115, '='));
    DBMS_OUTPUT.PUT_LINE('Total of Loans: ' || v_total_loan);
END;
/

EXEC prc_member_loan_detail('MEM0000011');